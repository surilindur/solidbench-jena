PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX snvoc: <http://localhost:3000/www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
SELECT (?title AS ?forumTitle) (SUM(?hasPost) AS ?postCount) WHERE {
  {
    SELECT DISTINCT ?fr ?forum WHERE {
      {
        SELECT DISTINCT ?fr ?frId WHERE {
          <http://localhost:3000/pods/00000015393162790207/profile/card#me> rdf:type snvoc:Person;
            snvoc:id ?rootId;
            ((((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson))?)/((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson))) ?fr.
          ?fr snvoc:id ?frId.
          FILTER(?frId != ?rootId)
        }
      }
      ?forum rdf:type snvoc:Forum;
        snvoc:hasMember ?mem.
      ?mem snvoc:hasPerson ?fr;
        snvoc:joinDate ?date.
      FILTER(?date >= "2012-08-11T00:00:00.000Z"^^xsd:dateTime)
    }
  }
  OPTIONAL {
    ?post rdf:type snvoc:Post;
      snvoc:hasCreator ?fr.
    ?forum snvoc:containerOf ?post.
    BIND(1  AS ?hasPostInner)
  }
  ?forum snvoc:title ?title;
    snvoc:id ?forumId.
  BIND(COALESCE(?hasPostInner, 0 ) AS ?hasPost)
}
GROUP BY ?title ?forumId
ORDER BY DESC (?postCount) (?forumId)
LIMIT 20
